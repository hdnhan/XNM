load "medit"

string path = "data/";
int nthfile = 3;
string filemesh = "Th" + nthfile;
mesh Th = readmesh(path + filemesh + ".msh");

mesh Thwet;

int[int] NoVMesh = [515, 1802, 6718];
real[int, int] hz(NoVMesh[nthfile - 1], 3);
ifstream filemeshZ(path + filemesh + "CoZ.txt");
filemeshZ >> hz;

real[string] mapz;
for (int i = 0; i < hz.n; i++){
    mapz[string(hz(i, 0)) + " " + string(hz(i, 1))] = hz(i, 2);
}

func real[int] FillZ(real[int] mat){
    for (int i = 0; i < Th.nv; i++){
        mat(i) = mapz[string(Th(i).x) + " " + string(Th(i).y)];
    }
    return mat;
}

real[int, int] data(503, 4);
ifstream filedata(path + "SLMIKE21-3.txt");
filedata >> data;

func real[int] InterpolationData(real t){
    // t: time (unit: second)
    real T = 3600;
    int n = t /  T;
    real delt = t / T - n;
    real[int] res(4);
    res(0) = data(n + 1, 0) * delt + data(n, 0) * (1 - delt);
    res(1) = data(n + 1, 1) * delt + data(n, 1) * (1 - delt);
    res(2) = data(n + 1, 2) * delt + data(n, 2) * (1 - delt);
    res(3) = data(n + 1, 3) * delt + data(n, 3) * (1 - delt);
    return res; 
}

real dt = 1e-2, nu = 2.67 * 1e-4, g = 9.81, rho = 1000, n = 0.025, T = 9999999;
real tau =  g * n * n;
real eps = 0.5, error = 2e-1;

macro Grad(u) [dx(u), dy(u)] //
macro Grad1(ux, uy) [dx(ux), dy(ux), dx(uy), dy(uy)] //
macro Grad2(ux, uy) [[dx(ux), dy(ux)], [dx(uy), dy(uy)]] //
macro Div(ux, uy) (dx(ux) + dy(uy)) //

fespace Xh(Th, P1);
fespace Hh(Th, P1dc);
Xh zh; zh[] = -FillZ(zh[]);
Hh H, phi, Hold = 9.0 + zh;
Xh ux = 0.0, uy = 0.0;
int cnt = 0;
string foldername = "result3/";

savemesh(Th, foldername + "ThH.mesh");
savemesh(Th, foldername + "ThU.mesh");

for(real t = dt; t <= T; t += dt){
    cout << "t: " << t << endl;
    real[int] BoundData = InterpolationData(t);
    real etaUB = BoundData(1);

    real al = 0.5;
    macro n() (N.x * ux + N.y * uy) //
    solve TransportEquation(H, phi) = 
        int2d(Th)((H/dt + ux * dx(H) + uy * dy(H) + H * Div(ux, uy)) * phi)
        + intalledges(Th)((1 - nTonEdge) * phi * (al * abs(n) - n/2) *  jump(H))
        - int1d(Th, 2)((n < 0) * abs(n) * H * phi)
        - int2d(Th)(Hold * phi / dt)
        + int1d(Th, 2)((n < 0) * abs(n) * (etaUB + zh) * phi);

    for(int i = 0; i < 4; i++)
    {
        Xh sign = (H >= eps);
        Th = adaptmesh(Th, sign, err = error, nbvx = 1e7, anisomax = 1);
        H = H;
        error = error/2;
    }

    Hold = H;

    Thwet = trunc(Th, H - eps >= 0);
    medit("Thwet", Thwet, H);

    fespace Xhwet(Thwet, P1);
    Xhwet uwx, uwy, vx, vy;
    Xhwet uxold = ux, uyold = uy;
    Xhwet eta = H - zh;
    
    real uUB = BoundData(0);
    solve NavierStokes([uwx, uwy], [vx, vy], solver = Crout) = 
        int2d(Thwet)((uwx * vx + uwy * vy) / dt)
        - int2d(Thwet)(
              convect([uxold, uyold], -dt, uxold) * vx / dt 
            + convect([uxold, uyold], -dt, uyold) * vy / dt)
        + int2d(Thwet)(g * Grad(eta)' * [vx, vy])
        - int2d(Thwet)(nu / H * [vx, vy]' * Grad2(uwx, uwy) * Grad(H))
        + int2d(Thwet)(nu * Grad1(uwx, uwy)' * Grad1(vx, vy))
        + int2d(Thwet)((tau / H^(4/3) * (uxold * uxold + uyold * uyold)^(0.5)) * (uwx * vx + uwy * vy))
        + on(2, uwx = uUB, uwy = 0) + on(1, uwx = 0, uwy = 0);

    ux = 0; uy = 0; // reset
    ux = uwx; uy = uwy;

    /*
    Xhwet psi, psit;

    solve streamlines(psi, psit) = 
        int2d(Thwet)(dx(psi)*dx(psit) + dy(psi)*dy(psit))
        + int2d(Thwet)(-psit*(dy(uwx) - dx(uwy)))
        + on(1, 2, 3, 4, psi = 0);
    */

    plot([uwx, uwy], fill = 1, value = 1);
    // plot(psi);

    savesol(foldername + "ThH." + string(cnt) + ".sol", Th, H);
	savemesh(Th, foldername + "ThH." + string(cnt)+".mesh");
    savesol(foldername + "ThU." + string(cnt) + ".sol", Th, [ux, uy]);
	savemesh(Th, foldername + "ThU." + string(cnt)+".mesh");
    cnt += 1;
}