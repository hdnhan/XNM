string filemesh = "Th2";
mesh Th = readmesh(filemesh + ".mesh");

fespace Xh(Th,P2); // definition of the velocity component space
fespace Mh(Th,P1); // definition of the pressure space
Xh u2, v2, u1, v1;
Xh u1old=0.5, u2old=0.5;
Mh p, q, vort;
macro Grad(u) [dx(u),dy(u)]//
macro div(u, v) (dx(u)+dy(v))//
real dt=0.1, mu=2.67 * 1e-4;
for(int i=0; i<500; i++){
	Xh u1oldtemp=convect([u1old,u2old],-dt,u1old);
	Xh u2oldtemp=convect([u1old,u2old],-dt,u2old);

	solve NavierStokes ([u1,u2,p],[v1,v2,q],solver = Crout)=
	int2d(Th)
	(
		1.0/dt*(u1*v1+u2*v2)
		+ mu*(Grad(u1)'*Grad(v1)+Grad(u2)'*Grad(v2))
	    - 1e-10*p*q
	    - p*div(v1, v2)
	    - q*div(u1, u2)
	)
	- int2d(Th)(1.0/dt*(u1oldtemp*v1+u2oldtemp*v2))
	+ on(1, u1 =0, u2 = 0) + on(2, u1 =1, u2 =0);

	u1old=u1;
	u2old=u2;

    plot([u1, u2]);
	/* 
	// streamline
	Xh psi,phi;
    solve streamlines(psi,phi) = 
        int2d(Th)( dx(psi)*dx(phi) + dy(psi)*dy(phi)) 
        + int2d(Th)(-phi * (dy(u1)-dx(u2))) + on(1, 3, 5, psi = 0)+on(4, psi=0);
	
	//vort=dy(u1) - dx(u2);
    //plot( vort, dim=2d, fill=0, wait=0);
    plot(psi, fill=true);*/
}